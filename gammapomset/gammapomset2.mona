#Traduction de la relation de successeurImmediat sur les gamma-pomset
pred relationGPomset(var1 x,y, var2 Pre0, Pre1, Post0, Post1, Mot) =
  x < y &
  (
    ( x in Post0 & y in Pre0 & (all1 z:(z>x & z<y) => z notin Post0)) |
    ( x in Post1 & y in Pre1 & (all1 z:(z>x & z<y) => z notin Post1))
  );

#Traduction de la relation d'ordre partiel
pred partialOrder(var1 x, y, var2 Pre0, Pre1, Post0, Post1, Mot) =
  all2 X: ( (x in X &
            (all1 u,v: (relationGPomset(u,v,Pre0,Pre1,Post0,Post1, Mot) & u in X)
            => v in X)) => y in X);

#Traduction de la relation de causalit√© sur les Pomsets
pred successeurImmediat(var1 x, y, var2 Pre0, Pre1, Post0, Post1, Mot) =
  x ~= y &
  partialOrder(x,y,Pre0,Pre1,Post0,Post1,Mot) & ~(ex1 z: z~=x & z~=y &
      (partialOrder(x,z,Pre0,Pre1,Post0, Post1,Mot) &
        partialOrder(z,y,Pre0,Pre1,Post0,Post1,Mot)
      )
  );

#    ---------------------------------------------------------
pred EXP( var2 RES, P, Pre0, Pre1, Post0, Post1, Mot) =
  all1 x : (
    (x in RES)
      <=>
    ( ex1 y: y in P & successeurImmediat(x,y,Pre0,Pre1,Post0,Post1, Mot))
  )
;

pred AXP( var2 RES, P, Pre0, Pre1, Post0, Post1, Mot)=
  EXP(Mot\RES, Mot\P,Pre0,Pre1,Post0,Post1,Mot)
;

pred pEUq ( var2 RES, P, Q, Pre0, Pre1, Post0, Post1, Mot)=
  all1 x : (
        x in Q => x in RES  &
        all1 y,z : (successeurImmediat(y,z,Pre0,Pre1,Post0,Post1, Mot) & y in P & z in RES) => y in RES
  )
;

pred pAUq ( var2 RES, P, Q, Pre0, Pre1, Post0, Post1, Mot)=
  all1 x :
      x in RES
      <=>
      (
        x in Q |
        ( x in P &
          all1 z :
            ~(successeurImmediat(x,z,Pre0,Pre1,Post0,Post1, Mot)) |
            (successeurImmediat(x,z,Pre0,Pre1,Post0,Post1, Mot) & z in RES)
        )
      );

pred pGUq(var2 RES,P,Q,Pre0,Pre1,Post0, Post1, Mot) =
  all1 x :
    x in RES
      <=>
    (
      x in Q |
      ( x in P &
        ex1 z: (z in Q & z>x &  partialOrder(x,z,Pre0,Pre1,Post0, Post1,Mot))
          & (all1 y:
              ( y>x & y<z & partialOrder(x,y,Pre0,Pre1,Post0, Post1,Mot) &
                partialOrder(y,z,Pre0,Pre1,Post0, Post1,Mot))
                => y in P
            )
      )
    )
;



#    ---------------------------------------------------------
pred EFP( var2 RES, P, Pre0, Pre1, Post0, Post1, Mot) =
  all1 x : (
    (x in RES)
      <=>
    ( ex1 y: y in P & partialOrder(x,y,Pre0,Pre1,Post0,Post1, Mot))
  )
;

pred EGP( var2 RES, P, Pre0, Pre1, Post0, Post1, Mot) =
all1 x : (
      (x in P & ~ex1 t : successeurImmediat(x,t,Pre0,Pre1,Post0,Post1, Mot)) => x in RES  &
      (all1 y,z : (successeurImmediat(y,z,Pre0,Pre1,Post0,Post1, Mot) & y in P & z in RES) => y in RES)
);

#    ---------------------------------------------------------

#NE FONCTIONNE PAS
pred AFP( var2 RES, P, Pre0, Pre1, Post0, Post1, Mot)=
  EGP(Mot\RES,Mot\P,Pre0,Pre1,Post0,Post1,Mot)
;

pred AGP( var2 RES, P, Pre0, Pre1, Post0, Post1, Mot)=
  EFP(Mot\RES, Mot\P, Pre0, Pre1, Post0, Post1, Mot)
;
